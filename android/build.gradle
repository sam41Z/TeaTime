apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'spoon'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 26
    buildToolsVersion "28.0.2"
    defaultConfig {
        applicationId "sam.teatime"
        minSdkVersion 26
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }


    flavorDimensions "fire", "store"

    productFlavors {

        withFirebase {
            dimension "fire"
        }

        noFirebase {
            dimension "fire"
        }

        forFDroid {
            dimension "store"
            buildConfigField 'String', 'STORE', '"fdroid"'
        }

        forAmazon {
            dimension "store"
            buildConfigField 'String', 'STORE', '"amazon"'
        }

        forPlay {
            dimension "store"
            buildConfigField 'String', 'STORE', '"play"'
        }
    }

    android.variantFilter { variant ->
        def fireBase = variant.getFlavors().get(0).name
        def store = variant.getFlavors().get(1).name

        variant.setIgnore((project.hasProperty("singleFlavor") && (store != 'forPlay') && (build != 'prod')) ||
                ((store == 'forAmazon' || store == 'forPlay') && fireBase == 'noFirebase') ||
                (store == 'forFDroid' && fireBase != 'noFirebase'))

    }


    packagingOptions {
        // needed for assertJ
        exclude 'asm-license.txt'
        exclude 'LICENSE'
        exclude 'NOTICE'

        // hack for instrumentation testing :-(
        exclude 'LICENSE.txt'

        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    lintOptions {
        warning 'MissingTranslation'
    }
}

configurations {

    // http://stackoverflow.com/questions/30578243/why-would-adding-espresso-contrib-cause-an-inflateexception
    androidTestCompile.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'design'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
    androidTestCompile.exclude group: 'com.android.support', module: 'preference-v7'
    androidTestCompile.exclude module: 'recyclerview-v7'
    androidTestCompile.exclude module: 'kotlin-stdlib'
}

dependencies {

    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:percent:$support_version"

    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"

    implementation 'com.github.ligi:ExtraCompats:0.4'
    implementation 'com.github.ligi:KAXT:0.18'

    withFirebaseImplementation 'com.google.firebase:firebase-core:11.4.0'

    androidTestImplementation "com.android.support:support-annotations:$support_version"
    androidTestImplementation "com.android.support:appcompat-v7:$support_version"
    androidTestImplementation "com.android.support:design:$support_version"

    androidTestImplementation 'com.github.ligi:trulesk:0.21'
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    kapt "android.arch.lifecycle:compiler:$lifecycle_version" // use kapt for Kotlin

    implementation "android.arch.persistence.room:runtime:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version" // use kapt for Kotlin

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.26.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.26.1'
}

if (hasProperty("gms")) {
    apply plugin: 'com.google.gms.google-services'
}

android.applicationVariants.all { variant ->
    if (!variant.name.contains("oFirebase")) {
        project.tasks.each { t ->
            if (t.name.contains("GoogleServices")) {
                // Remove google services plugin
                variant.getVariantData().resourceGenTask.getTaskDependencies().values.remove(t);
            }
        }
    }
}

spoon {
    debug = true
    grantAllPermissions = true
}